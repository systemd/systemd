/* SPDX-License-Identifier: LGPL-2.1-or-later */
%{
_Pragma("GCC diagnostic ignored \"-Wimplicit-fallthrough\"")
#if __GNUC__ >= 15
_Pragma("GCC diagnostic ignored \"-Wzero-as-null-pointer-constant\"")
#endif
#include <stddef.h>
#include "conf-parser.h"
#include "journald-config.h"
%}
struct ConfigPerfItem;
%null_strings
%language=ANSI-C
%define slot-name section_and_lvalue
%define hash-function-name journald_gperf_hash
%define lookup-function-name journald_gperf_lookup
%readonly-tables
%omit-struct-type
%struct-type
%includes
%%
Journal.Storage,              config_parse_storage,           0, offsetof(JournalConfig, storage)
Journal.Compress,             config_parse_compress,          0, offsetof(JournalConfig, compress)
Journal.Seal,                 config_parse_tristate,          0, offsetof(JournalConfig, seal)
Journal.ReadKMsg,             config_parse_tristate,          0, offsetof(JournalConfig, read_kmsg)
Journal.Audit,                config_parse_audit_set_mode,    0, offsetof(JournalConfig, set_audit)
Journal.SyncIntervalSec,      config_parse_sec,               0, offsetof(JournalConfig, sync_interval_usec)
# The following is a legacy name for compatibility
Journal.RateLimitInterval,    config_parse_sec,               0, offsetof(JournalConfig, ratelimit_interval)
Journal.RateLimitIntervalSec, config_parse_sec,               0, offsetof(JournalConfig, ratelimit_interval)
Journal.RateLimitBurst,       config_parse_unsigned,          0, offsetof(JournalConfig, ratelimit_burst)
Journal.SystemMaxUse,         config_parse_iec_uint64,        0, offsetof(JournalConfig, system_storage_metrics.max_use)
Journal.SystemMaxFileSize,    config_parse_iec_uint64,        0, offsetof(JournalConfig, system_storage_metrics.max_size)
Journal.SystemKeepFree,       config_parse_iec_uint64,        0, offsetof(JournalConfig, system_storage_metrics.keep_free)
Journal.SystemMaxFiles,       config_parse_uint64,            0, offsetof(JournalConfig, system_storage_metrics.n_max_files)
Journal.RuntimeMaxUse,        config_parse_iec_uint64,        0, offsetof(JournalConfig, runtime_storage_metrics.max_use)
Journal.RuntimeMaxFileSize,   config_parse_iec_uint64,        0, offsetof(JournalConfig, runtime_storage_metrics.max_size)
Journal.RuntimeKeepFree,      config_parse_iec_uint64,        0, offsetof(JournalConfig, runtime_storage_metrics.keep_free)
Journal.RuntimeMaxFiles,      config_parse_uint64,            0, offsetof(JournalConfig, runtime_storage_metrics.n_max_files)
Journal.MaxRetentionSec,      config_parse_sec,               0, offsetof(JournalConfig, max_retention_usec)
Journal.MaxFileSec,           config_parse_sec,               0, offsetof(JournalConfig, max_file_usec)
Journal.ForwardToSyslog,      config_parse_tristate,          0, offsetof(JournalConfig, forward_to_syslog)
Journal.ForwardToKMsg,        config_parse_tristate,          0, offsetof(JournalConfig, forward_to_kmsg)
Journal.ForwardToConsole,     config_parse_tristate,          0, offsetof(JournalConfig, forward_to_console)
Journal.ForwardToWall,        config_parse_tristate,          0, offsetof(JournalConfig, forward_to_wall)
Journal.ForwardToSocket,      config_parse_forward_to_socket, 0, offsetof(JournalConfig, forward_to_socket)
Journal.TTYPath,              config_parse_path,              0, offsetof(JournalConfig, tty_path)
Journal.MaxLevelStore,        config_parse_log_level,         0, offsetof(JournalConfig, max_level_store)
Journal.MaxLevelSyslog,       config_parse_log_level,         0, offsetof(JournalConfig, max_level_syslog)
Journal.MaxLevelKMsg,         config_parse_log_level,         0, offsetof(JournalConfig, max_level_kmsg)
Journal.MaxLevelConsole,      config_parse_log_level,         0, offsetof(JournalConfig, max_level_console)
Journal.MaxLevelWall,         config_parse_log_level,         0, offsetof(JournalConfig, max_level_wall)
Journal.MaxLevelSocket,       config_parse_log_level,         0, offsetof(JournalConfig, max_level_socket)
Journal.SplitMode,            config_parse_split_mode,        0, offsetof(JournalConfig, split_mode)
Journal.LineMax,              config_parse_line_max,          0, offsetof(JournalConfig, line_max)
